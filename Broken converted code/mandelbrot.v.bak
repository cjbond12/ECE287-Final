// FIXED POINT CONVENTION (32 bits)
// | 10 integer bits | 22 fraction bits |
`define INT(n) n[31:22]
`define FRAC(n) n[21:0]

`define HIGH_RES

`ifdef HIGH_RES
`define WIDTH 16'd320
`define HEIGHT 16'd240
`else
`define WIDTH 16'd160
`define HEIGHT 16'd120
`endif

module mandelbrot(
    input clk,
    input rstn,
    input start,
    output done,

    // Zoom and position control
    input [2:0] zoom_level,
    input [2:0] h_offset_level,
    input [2:0] v_offset_level,

    `ifdef HIGH_RES
    output [8:0] vga_x,
    output [7:0] vga_y,
    `else
    output [7:0] vga_x,
    output [6:0] vga_y,
    `endif

    output [2:0] vga_colour,
    output vga_plot
);

    // ======= [ COMB CONST & COMB CONST COMPUTATIONS ] =======
    wire signed [31:0] w, h, xmin, xmax, ymin, ymax, dx, dy;
    wire signed [31:0] h_offset, v_offset;

    // Computing the combination const values
    reg signed [31:0] w_reg, h_offset_reg, v_offset_reg;
    always @(*) begin
        case (zoom_level)
            3'b000: w_reg = {10'd10, 22'b0};        // 10
            3'b001: w_reg = {10'd4, 22'b0};         // 4
            3'b010: w_reg = {10'd2, 22'b0};         // 2
            3'b011: w_reg = {10'd1, 22'b0};         // 1
            3'b100: w_reg = {10'b0, 2'b11, 20'b0};  // 0.75
            3'b101: w_reg = {10'b0, 1'b1, 21'b0};   // 0.5
            3'b110: w_reg = {10'b0, 3'b011, 19'b0}; // 0.375
            3'b111: w_reg = {10'b0, 2'b01, 20'b0};  // 0.25
        endcase
    end
    assign w = w_reg;

    multiplier M0(.a(w), .b({10'b0, 2'b11, 20'b0}), .out(h));

    always @(*) begin
        case (h_offset_level)
            3'b000: h_offset_reg = {10'd3, 1'b1, 21'b0};
            3'b001: h_offset_reg = {10'd2, 2'b11, 20'b0};
            3'b010: h_offset_reg = {10'd2, 22'b0};
            3'b011: h_offset_reg = {10'd1, 1'b1, 22'b0};
            3'b100: h_offset_reg = {10'd1, 22'b0};
            3'b101: h_offset_reg = {10'd0, 2'b01, 20'b0};
            3'b110: h_offset_reg = {10'd0, 22'b0};
            3'b111: h_offset_reg = {10'b1111111111, 1'b1, 21'b0};
        endcase
    end
    assign h_offset = h_offset_reg;

    assign xmin = ((w + h_offset) >> 1) * ~(32'b0);
    assign xmax = xmin + w;

    always @(*) begin
        case (v_offset_level)
            3'b000: v_offset_reg = {10'd2, 22'b0};
            3'b001: v_offset_reg = {10'd1, 1'b1, 21'b0};
            3'b010: v_offset_reg = {10'd1, 22'b0};
            3'b011: v_offset_reg = {10'd0, 1'b1, 22'b0};
            3'b100: v_offset_reg = {10'd0, 22'b0};
            3'b101: v_offset_reg = {10'b1111111111, 1'b1, 21'b0};
            3'b110: v_offset_reg = {10'b1111111111, 22'b0};
            3'b111: v_offset_reg = {10'b1111111110, 1'b1, 21'b0};
        endcase
    end
    assign v_offset = v_offset_reg;

    assign ymin = ((h + v_offset) >> 1) * ~(32'b0);
    assign ymax = ymin + h;

    multiplier M_DX(
        .a(xmax - xmin),
        `ifdef HIGH_RES
        .b({10'b0, 22'b0000000011001100110011}),
        `else
        .b({10'b0, 22'b0000000110011001100110}),
        `endif
        .out(dx)
    );

    multiplier M_DY(
        .a(ymax - ymin),
        `ifdef HIGH_RES
        .b({10'b0, 22'b0000000100010001000100}),
        `else
        .b({10'b0, 22'b0000001000100010001000}),
        `endif
        .out(dy)
    );

    // ====== [ CONSTS ] ======
    wire [15:0] iterations = 16'd32;
    wire signed [31:0] max_distance = {10'd16, 22'b0};

    // ====== [ REGISTERS ] ======
    wire enx, eny, ena, enb, enn, eni, enj;
    wire signed [31:0] x, y, a, b;
    wire signed [31:0] x_new, y_new, a_new, b_new;
    wire [15:0] n, i, j;
    wire [15:0] n_new, i_new, j_new;

    register REG_X(.d(x_new), .q(x), .en(enx), .clk(clk));
    register REG_Y(.d(y_new), .q(y), .en(eny), .clk(clk));
    register REG_A(.d(a_new), .q(a), .en(ena), .clk(clk));
    register REG_B(.d(b_new), .q(b), .en(enb), .clk(clk));
    register #(16) REG_N(.d(n_new), .q(n), .en(enn), .clk(clk));
    register #(16) REG_I(.d(i_new), .q(i), .en(eni), .clk(clk));
    register #(16) REG_J(.d(j_new), .q(j), .en(enj), .clk(clk));

    // ====== [ LOOP INTERMEDIATE CALC ] ======
    wire signed [31:0] aa, bb, ab, twoab, distance;
    multiplier M_AA(.a(a), .b(a), .out(aa));
    multiplier M_BB(.a(b), .b(b), .out(bb));
    multiplier M_AB(.a(a), .b(b), .out(ab));

    assign twoab = 32'd2 * ab;
    assign distance = aa + bb;

    // ====== [ COMBINATIONAL NEXT VALUE LOGIC ] ======
    wire initx, inity, inita, initb, initn, initi, initj;
    assign x_new = initx ? xmin : x + dx;
    assign y_new = inity ? ymin : y + dy;
    assign a_new = inita ? x : aa - bb + x;
    assign b_new = initb ? y : twoab + y;
    assign n_new = initn ? 16'd0 : n + 16'd1;
    assign i_new = initi ? 16'd0 : i + 16'd1;
    assign j_new = initj ? 16'd0 : j + 16'd1;

    // ====== [ BOOLEAN STATE MACHINE INPUT ] ======
    wire n_equals_iterations = n_new == iterations;
    wire n_lt_iterations = n_new < iterations;
    wire donei = i_new == `WIDTH;
    wire donej = j_new == `HEIGHT;
    wire dist_gt_max_dist = distance > max_distance;

    // ====== [ STATE MACHINE ] ======
    statemachine SM(
        .clk(clk),
        .rst(rstn),
        .start(start),
        .n_equals_iterations(n_equals_iterations),
        .n_lt_iterations(n_lt_iterations),
        .donei(donei),
        .donej(donej),
        .dist_gt_max_dist(dist_gt_max_dist),
        .enx(enx),
        .eny(eny),
        .ena(ena),
        .enb(enb),
        .enn(enn),
        .eni(eni),
        .enj(enj),
        .initx(initx),
        .inity(inity),
        .inita(inita),
        .initb(initb),
        .initn(initn),
        .initi(initi),
        .initj(initj),
        .plot(vga_plot),
        .done(done)
    );

    // ====== [ VGA OUTPUT ] ======
    `ifdef HIGH_RES
    assign vga_x = i[8:0];
    assign vga_y = j[7:0];
    `else
    assign vga_x = i[7:0];
    assign vga_y = j[6:0];
    `endif
    assign vga_colour = n_equals_iterations ? 3'b000 : n[2:0];

endmodule
